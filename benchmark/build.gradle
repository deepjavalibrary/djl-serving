plugins {
    id 'application'
    id "nebula.ospackage" version "9.0.0"
}

dependencies {
    implementation "commons-cli:commons-cli:${commons_cli_version}"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_slf4j_version}"
    implementation platform("ai.djl:bom:${project.version}")
    implementation project(":wlm")

    implementation "ai.djl:model-zoo"
    runtimeOnly "ai.djl.pytorch:pytorch-model-zoo"
    runtimeOnly "ai.djl.tensorflow:tensorflow-model-zoo"
    runtimeOnly "ai.djl.mxnet:mxnet-model-zoo"
    runtimeOnly "ai.djl.paddlepaddle:paddlepaddle-model-zoo"
    runtimeOnly "ai.djl.tflite:tflite-engine"
    runtimeOnly "ai.djl.dlr:dlr-engine"
    runtimeOnly "ai.djl.ml.xgboost:xgboost"
    runtimeOnly project(":engines:python")
    runtimeOnly "ai.djl.tensorrt:tensorrt"

    testImplementation("org.testng:testng:${testng_version}") {
        exclude group: "junit", module: "junit"
    }

    ProcessBuilder pb = new ProcessBuilder("nvidia-smi", "-L")
    def hasGPU = false;
    try {
        Process process = pb.start()
        hasGPU = process.waitFor() == 0
    } catch (IOException ignore) {
    }

    if (hasGPU) {
        runtimeOnly("ai.djl.onnxruntime:onnxruntime-engine") {
            exclude group: "com.microsoft.onnxruntime", module: "onnxruntime"
        }
        runtimeOnly "com.microsoft.onnxruntime:onnxruntime_gpu:${onnxruntime_version}"
    } else {
        runtimeOnly "ai.djl.onnxruntime:onnxruntime-engine"
    }
}

application {
    mainClass = System.getProperty("main", "ai.djl.benchmark.Benchmark")
}

run {
    environment("TF_CPP_MIN_LOG_LEVEL", "1") // turn off TensorFlow print out
    systemProperties System.getProperties()
    systemProperties.remove("user.dir")
    systemProperty("file.encoding", "UTF-8")
}

task benchmark(type: JavaExec) {
    environment("TF_CPP_MIN_LOG_LEVEL", "1") // turn off TensorFlow print out
    List<String> arguments = gradle.startParameter["taskRequests"]["args"].getAt(0)
    for (String argument : arguments) {
        if (argument.trim().startsWith("--args")) {
            String[] line = argument.split("=", 2)
            if (line.length == 2) {
                line = line[1].split(" ")
                if (line.contains("-t")) {
                    if (System.getProperty("ai.djl.default_engine") == "TensorFlow") {
                        environment("OMP_NUM_THREADS", "1")
                        environment("TF_NUM_INTRAOP_THREADS", "1")
                    } else {
                        environment("MXNET_ENGINE_TYPE", "NaiveEngine")
                        environment("OMP_NUM_THREADS", "1")
                    }
                }
                break
            }
        }
    }

    systemProperties System.getProperties()
    systemProperties.remove("user.dir")
    systemProperty("file.encoding", "UTF-8")
    classpath = sourceSets.main.runtimeClasspath
    // restrict the jvm heap size for better monitoring benchmark
    jvmArgs = ["-Xmx2g"]
    if (Boolean.getBoolean("loggc")) {
        if (JavaVersion.current() == JavaVersion.VERSION_1_8) {
            jvmArgs += ["-XX:+PrintGCTimeStamps", "-Xloggc:build/gc.log"]
        } else {
            jvmArgs += ["-Xlog:gc*=debug:file=build/gc.log"]
        }
    }
    mainClass = "ai.djl.benchmark.Benchmark"
}

task createDeb(type: Deb, dependsOn: distTar) {
    doFirst {
        exec {
            commandLine "tar", "xvf", "${project.buildDir}/distributions/benchmark-${project.version}.tar", "-C", "${project.buildDir}"
        }
    }

    packageName = "djl-bench"
    archiveVersion = "${project.version}"
    release = 1
    maintainer = "Deep Java Library <djl-dev@amazon.com>"
    summary = "djl-bench is a command line tool that allows you to benchmark the\n" +
            "  model on all different platforms for single-thread/multi-thread\n" +
            "  inference performance."

    from("${project.buildDir}/benchmark-${project.version}") {
        into "/usr/local/djl-bench-${project.version}"
    }
    link("/usr/bin/djl-bench", "/usr/local/djl-bench-${project.version}/bin/benchmark")
}

task installOnLinux(type: Exec, dependsOn: createDeb) {
    doFirst {
        if (System.properties['os.name'].startsWith("Linux")) {
            String ver = "${project.version}".replaceAll("-", "~")
            commandLine "sudo", "dpkg", "-i", "${project.buildDir}/distributions/djl-bench_${ver}-1_all.deb"
        } else {
            throw new GradleException("task installOnLinux Only supported on Linux.")
        }
    }
}

startScripts {
    defaultJvmOpts = []
    doLast {
        String replacement = 'CLASSPATH=\\$APP_HOME/lib/*\n\n' +
                'if [[ "\\$*" == *-t* || "\\$*" == *--threads* ]]\n' +
                'then\n' +
                '    export TF_CPP_MIN_LOG_LEVEL=1\n' +
                '    export MXNET_ENGINE_TYPE=NaiveEngine\n' +
                '    export OMP_NUM_THREADS=1\n' +
                '    export TF_NUM_INTRAOP_THREADS=1\n' +
                'fi'

        String text = unixScript.text.replaceAll('CLASSPATH=\\$APP_HOME/lib/.*', replacement)
        text = text.replaceAll("/usr/bin/env sh", "/usr/bin/env bash")
        text = text.replaceAll("#!/bin/sh", "#!/bin/bash")

        unixScript.text = text
    }
}
