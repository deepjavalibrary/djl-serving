plugins {
    id "java"
}

dependencies {
    implementation platform("ai.djl:bom:${project.version}")
    implementation ("ai.djl.huggingface:tokenizers") {
        exclude group: "org.apache.commons", module: "commons-compress"
    }
    implementation "org.slf4j:slf4j-simple:${slf4j_version}"
    implementation "commons-cli:commons-cli:${commons_cli_version}"
    implementation "commons-codec:commons-codec:1.16.0"
    implementation "commons-io:commons-io:2.15.1"
    implementation "com.google.code.gson:gson:2.10.1"
    implementation "org.apache.httpcomponents:httpclient:4.5.13"
    implementation "org.apache.httpcomponents:httpmime:4.5.13"

    testImplementation "io.netty:netty-codec-http:${netty_version}"
    testImplementation("org.testng:testng:${testng_version}") {
        exclude group: "junit", module: "junit"
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'ai.djl.awscurl.AwsCurl'
    }
    includeEmptyDirs = false
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from configurations.runtimeClasspath.collect {
        it.isDirectory() ? it : zipTree(it).matching {
            exclude "**/*.so"
            exclude "**/*.dylib"
            exclude "**/*.dll"
        }
    }
}

jar.doLast {
    exec {
        workingDir "."
        executable "sh"
        args "-c", "cat src/main/scripts/stub.sh build/libs/awscurl*.jar > build/awscurl && chmod +x build/awscurl"
        //commandLine "sh -c 'cat src/main/scripts/stub.sh build/libs/awscurl*.jar > build/awscurl && chmod +x build/awscurl'"
    }
}
